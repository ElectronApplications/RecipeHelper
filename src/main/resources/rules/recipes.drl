package recipe_system

import org.example.IngredientAmountType
import org.example.IngredientDescription
import org.example.Ingredient
import org.example.Recipe
import org.example.UnknownIngredient
import org.example.IngredientInfo
import org.example.UserRecipe

dialect "mvel"

declare UserIngredient
    ingredient : String
    has : Boolean
end

declare RecipeIngredient
    recipe : Recipe
    ingredient : String
    amount : Double
end

query "Unknown Ingredients"
    $ingredient: UnknownIngredient()
end

query "User Recipes"
    $recipe : UserRecipe()
end

rule "Initialize Recipe Ingredients"
    when
        $recipe : Recipe()
        $ingredient : Ingredient() from $recipe.ingredients
        not RecipeIngredient(recipe == $recipe, ingredient == $ingredient.ingredient)
    then
        insert(new RecipeIngredient($recipe, $ingredient.ingredient, $ingredient.amount))
end

rule "Initialize User Ingredients"
    when
        $ingredient : IngredientDescription()
        not UserIngredient(ingredient == $ingredient.id)
    then
        insert(new UserIngredient($ingredient.id, null))
end

rule "Initialize User Recipes"
    when
        $recipe : Recipe()
        not UserRecipe(recipe == $recipe)
    then
        insert(new UserRecipe($recipe, null))
end

rule "Initialize Unknown Ingredients"
    when
        $userIngredient: UserIngredient(has == null)
        $weight : Double() from accumulate(
            RecipeIngredient(ingredient == $userIngredient.ingredient),
            count()
        )
        not UnknownIngredient(ingredient == $userIngredient.ingredient)
    then
        insert(new UnknownIngredient($userIngredient.ingredient, $weight))
end

rule "Process new ingredient info"
    when
        $ingredientInfo: IngredientInfo()
        $userIngredient: UserIngredient(ingredient == $ingredientInfo.ingredient)
    then
        $userIngredient.has = $ingredientInfo.has
        update($userIngredient)
        delete($ingredientInfo)
end

rule "Remove unknown ingredient"
    when
        $userIngredient: UserIngredient(has != null)
        $unknownIngredient: UnknownIngredient(ingredient == $userIngredient.ingredient)
    then
        delete($unknownIngredient)
end

rule "Mark user recipe as not possible if an ingredient is missing"
    when
        $userRecipe: UserRecipe(possible != false)
        $recipeIngredient: RecipeIngredient(recipe == $userRecipe.recipe)
        UserIngredient(ingredient == $recipeIngredient.ingredient, has == false)
    then
        $userRecipe.possible = false
        update($userRecipe)
end

rule "Mark user recipe as possible if no ingredient is missing"
    when
        $userRecipe: UserRecipe(possible != true)
        not (
            $recipeIngredient: RecipeIngredient(recipe == $userRecipe.recipe)
            and
            UserIngredient(ingredient == $recipeIngredient.ingredient, has != true)
        )
    then
        $userRecipe.possible = true
        update($userRecipe)
end

